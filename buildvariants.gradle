def generateBuildConfigVariables(variant) {
    def flavorName = variant.flavorName
    def propFile = file("../.config/${flavorName}.properties")
    if (propFile.canRead()) {
        Properties props = new Properties()
        props.load(new FileInputStream(propFile))
        if (props != null) {
            def buildTypePostfix = variant.buildType.name == "debug" ? "_DEBUG" : ""
            variant.buildConfigField "String", "HOST_API", "\"" + props['HOST_API' + buildTypePostfix] + "\""
            variant.buildConfigField "String", "HOST_NAME", "\"" + props['HOST_NAME' + buildTypePostfix] + "\""
        }
    } else {
        println "Missing app properties file"
    }
}

def appendVersionNameVersionCode(variant) {
    variant.outputs.all {
        outputFileName = "${variant.name}-${variant.versionName}.apk"
    }
}

ext {
    generateBuildConfigVariables = this.&generateBuildConfigVariables
    appendVersionNameVersionCode = this.&appendVersionNameVersionCode
}
